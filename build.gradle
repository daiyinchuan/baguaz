allprojects {
	apply plugin: 'java'

	[compileJava]*.options*.encoding = 'UTF-8'
	
	sourceCompatibility = 1.8
	
	repositories {
		mavenLocal()
		jcenter()
		maven{ url 'http://maven.oschina.net/content/groups/public/'}
		mavenCentral()
	}
}

apply plugin:'war'

apply plugin: 'application'
mainClassName = 'com.baguaz.cms.AppConfig'

webAppDirName = 'src/main/webapp'

/*
 * 从version.properties获取version
 */
def bp=new Properties()
def loadProperties={
	def bpPath=sourceSets.main.resources.srcDirs[0].toString()+File.separator+"version.properties"
	def bf=file(bpPath)
	bf.withInputStream{
		stream->bp.load(stream)
	}
}
loadProperties()

/*
 * 只打包class文件到jar包
 */
task jarWithoutResources(type: Jar) {  
    baseName project.name
    version  bp.getProperty("version")
    from("$buildDir/classes/main")  
}

war{
	dependsOn jarWithoutResources
	
	//打包资源文件到war包的WEB-INF/classes路径
	from("$buildDir/resources/main") {  
        include "*"  
        into("WEB-INF/classes")  
    }
	/*
	 * 主版本号：做了不兼容的API 修改，如整体风格变化、大规模重构等；
	 * 次版本号：做了向下兼容的功能性新增；
	 * 修订号：做了向下兼容的问题修正、细节调整等。
	 */
	version bp.getProperty("version")
	
	//类路径排除class文件和资源文件
	classpath=classpath - sourceSets.main.output
	
    classpath fileTree(dir:libsDir, include:"${project.name}-${version}.jar")  
}

configurations {
	compile.transitive = false
}

dependencies{
	providedCompile(
		'org.eclipse.jetty:jetty-server:8.0.4.v20111024',
		'org.eclipse.jetty:jetty-webapp:8.0.4.v20111024',
		'javax.servlet.jsp:jsp-api:2.2',
		'javax.el:el-api:2.2',
	)
	compile(
		'org.beetl:beetl-core:2.2.3',
		'com.alibaba:druid:1.0.5',
		'mysql:mysql-connector-java:5.1.34',
		'org.slf4j:slf4j-api:1.7.10',
		'ch.qos.logback:logback-core:1.1.2',
		'ch.qos.logback:logback-classic:1.1.2',
		'org.slf4j:jcl-over-slf4j:1.7.10',
		'org.slf4j:log4j-over-slf4j:1.7.10',
		'org.codehaus.groovy:groovy-all:2.3.6',
		'net.sf.ehcache:ehcache-core:2.6.11',
		'org.apache.shiro:shiro-core:1.2.4',
		'org.apache.shiro:shiro-web:1.2.4',
		'org.apache.shiro:shiro-ehcache:1.2.4',
		'org.apache.commons:commons-lang3:3.3.2',
		'com.google.guava:guava:18.0',
		'com.alibaba:fastjson:1.2.7',
		'commons-fileupload:commons-fileupload:1.3.1',
		'oro:oro:2.0.8',
		'commons-codec:commons-codec:1.10',
		'cglib:cglib-nodep:3.1',
		'redis.clients:jedis:2.7.2',
		'de.ruedigermoeller:fst:2.29',
		'com.jfinal:cos:26Dec2008',
		'junit:junit:4.11',
		'org.elasticsearch:elasticsearch:2.3.1',
		'org.apache.commons:commons-pool2:2.3',
		'net.coobird:thumbnailator:0.4.8',
		'javax.mail:mail:1.4.7',
		'org.apache.activemq:artemis-server:1.3.0',
		'org.apache.activemq:artemis-jms-server:1.3.0',
		'org.apache.activemq:artemis-jms-client:1.3.0',
		'it.sauronsoftware.cron4j:cron4j:2.2.5'
	)
}

task wrapper(type:Wrapper){
	gradleVersion='2.12'
}
